#!/usr/bin/perl
use strict;
use warnings;
use Time::HiRes qw(sleep time);

my $script = "/opt/qtbigtext/qtbigtext.py";

my $dbusTimeoutMillis = 1000;
my $dbusRetryDelayMillis = 100;
my $dbusRetryMaxTimeoutMillis = 10000;

my @PYTHON_EXEC_NAMES = qw(python3 python2.7 python2.6 python2 python);

sub python();
sub testPythonPyQt5($);
sub killBigText();
sub isRunning();
sub ensureDbus();
sub dbusTest();
sub dbusSetText($);
sub dbusSend($@);
sub setBigText($$);
sub restartBigtext($$);

my $usage = "Usage:
  $0 -h|--help
     Show this message

  $0 [OPTS] -k
     Kill qtbigtext

  $0 [OPTS] --ensure-dbus
     check for dbus service every ${dbusTimeoutMillis}ms
     exit when it succeeds, OR after ${dbusRetryMaxTimeoutMillis}ms

  $0 [OPTS] --wait [TEXT TEXT ...]
  $0 [OPTS] --wait FILE_NAME
    invoke qtbigtext directly and wait for it to end

  $0 [OPTS] --stdin
     Read lines from stdin, backslash interpolate them,
       and call qtbigtext for each parsed line

  $0 [OPTS] FILE_NAME
     Read FILE_NAME and call qtbigtext with the contents

  $0 [OPTS] [TEXT TEXT ...]
     Call qtbigtxt with \"TEXT TEXT ...\"

  qtbigtext is called with dbus-send if its running,
    or invoked directly otherwise.

  OPTS
    OPTS are passed into qtbigtext only when invoked directly, NOT via dbus cmds.
      --KEY=VAL  any KEY and VAL (including empty VAL)
      -f         same as: --fullscreen=true
      -w         same as: --fullscreen=false
      <W>x<H>    same as: --forceWidth=<W>  --forceHeight=<H>
";

sub main(@){
  die $usage if @_ == 1 and $_[0] =~ /^(-h|--help)$/;

  my $opts = [];
  while(@_ > 0 and $_[0] =~ /^(--[a-zA-Z0-9_\-]+=.*|-f|-w|\d+x\d+)$/){
    my $opt = shift;
    if($opt =~ /^(--[a-zA-Z0-9_\-]+=.*)$/){
      push @$opts, $opt;
    }elsif($opt =~ /^(-f)$/){
      push @$opts, "--fullScreen=true";
    }elsif($opt =~ /^(-w)$/){
      push @$opts, "--fullScreen=false";
    }elsif($opt =~ /^(\d+)x(\d+)$/){
      push @$opts, "--forceWidth=$1";
      push @$opts, "--forceHeight=$2";
    }else{
      die "ERROR: malformed opt: $opt\n";
    }
  }

  my $cmd = '';
  $cmd = shift if @_ > 0 and $_[0] =~ /^(-k|--ensure-dbus|--stdin|--wait)$/;

  if($cmd eq "-k"){
    die $usage if @_ > 0;
    killBigText();
  }elsif($cmd eq "--ensure-dbus"){
    ensureDbus();
  }elsif($cmd eq "--stdin"){
    die $usage if @_ > 0;
    my $line;
    while($line = <STDIN>){
      my $str = $line;
      chomp $str;
      my %backslashReplace = (
        "\\" => "\\",
        "t" => "\t",
        "n" => "\n",
        "r" => "",
      );
      $str =~ s/\\([\\tn])/$backslashReplace{$1}/g;
      setBigText $str, $opts if $str ne "";
    }
  }elsif($cmd eq "--wait"){
    my $text;
    if(@_ == 1 and $_[0] !~ /\n/ and -e $_[0]){
      my $file = shift;
      open FH, "< $file";
      $text = join '', <FH>;
      close FH;
    }else{
      $text = "@_";
    }
    my $python = python();
    exec $python, $script, @$opts, $text;
  }else{
    my $text;
    if(@_ == 1 and $_[0] !~ /\n/ and -e $_[0]){
      my $file = shift;
      open FH, "< $file";
      my $contents = join '', <FH>;
      close FH;
      $text = $contents;
    }else{
      $text = "@_";
    }
    setBigText $text, $opts;
  }
}

sub python(){
  my $python = '';
  for my $pythonExecName(@PYTHON_EXEC_NAMES){
    my $pythonExec = `which $pythonExecName`;
    chomp $pythonExec;
    if($pythonExec =~ /python/ and testPythonQt5($pythonExec)){
      return $pythonExec;
    }
  }
  die "ERROR: could not import PyQt5 using any of: @PYTHON_EXEC_NAMES\n";
}

sub testPythonQt5($){
  my ($pythonExec) = @_;
  my $testOut = `$pythonExec -c 'import PyQt5; print("PyQt5 FOUND")' 2>&1`;
  if($testOut =~ /PyQt5 FOUND/){
    return 1;
  }else{
    return 0;
  }
}

sub killBigText(){
  my $python = python();
  system "pkill", "-f", "^$python.*$script";
}

sub isRunning(){
  my $python = python();
  system "pkill", "-0", "-f", "^$python.*$script";
  return $? == 0;
}

sub ensureDbus(){
  print "\nTESTING DBUS\n";
  my $startMillis = int(time * 1000.0);
  while(not dbusTest){
    my $nowMillis = int(time * 1000.0);
    if($nowMillis - $startMillis > $dbusRetryMaxTimeoutMillis){
      die "FATAL: qtbigtext dbus failed to start\n";
    }
    sleep $dbusRetryDelayMillis/1000;
  }
  print "SUCCESS\n\n";
}
sub dbusTest(){
  return dbusSend "test";
}
sub dbusSetText($){
  my $str = shift;
  return dbusSend "setText", "string:$str";
}
sub dbusSend($@){
  my ($method, @args) = @_;
  my $dest = "org.teleshoes.qtbigtext";
  my $path = "/";
  my @cmd = ("dbus-send",
    "--session",
    "--print-reply",
    "--type=method_call",
    "--reply-timeout=$dbusTimeoutMillis",
    "--dest=$dest",
    $path,
    "$dest.$method",
    @args,
  );
  system @cmd;
  return $? == 0;
}

sub setBigText($$){
  my $str = shift;
  my $opts = shift;

  if(isRunning and dbusTest){
    if(dbusSetText $str){
      print "dbus succeeded\n";
    }else{
      print "dbus failed, restarting\n";
      restartBigtext $str, $opts;
    }
  }else{
    print "not running or stalled, restarting\n";
    restartBigtext $str, $opts;
  }
}

sub restartBigtext($$){
  my ($str, $opts) = @_;
  print "killing $script\n";
  killBigText();
  print "(re)running $script\n";
  my $python = python;
  $str =~ s/'/'\\''/g;
  system "$python $script @$opts '$str' &";

  ensureDbus();
}

&main(@ARGV);
