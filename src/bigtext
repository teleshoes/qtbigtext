#!/usr/bin/perl
use strict;
use warnings;

my $script = "/opt/qtbigtext/qtbigtext.py";

my $dbusRetries = 10;
my $retryDelay = 1;

sub python();
sub killBigText();
sub isRunning();
sub dbusCall($);
sub setBigText($);

my $usage = "Usage:
  $0 -h|--help
     Show this message

  $0 -k
     Kill qtbigtext

  $0 --stdin
     Read lines from stdin, backslash interpolate them,
       and call qtbigtext for each parsed line

  $0 FILE_NAME
     Read FILE_NAME and call qtbigtext with the contents

  $0 [TEXT TEXT ...]
     Call qtbigtxt with \"TEXT TEXT ...\"

  qtbigtext is called with dbus-send if its running,
    or invoked directly otherwise.
";

sub main(@){
  die $usage if @_ == 1 and $_[0] =~ /^(-h|--help)$/;
  my $cmd = '';
  $cmd = shift if @_ > 0 and $_[0] =~ /^(-k|--stdin)$/;
  if($cmd eq "-k"){
    die $usage if @_ > 0;
    killBigText();
  }elsif($cmd eq "--stdin"){
    die $usage if @_ > 0;
    my $line;
    while($line = <STDIN>){
      my $str = $line;
      chomp $str;
      my %backslashReplace = (
        "\\" => "\\",
        "t" => "\t",
        "n" => "\n",
        "r" => "",
      );
      $str =~ s/\\([\\tn])/$backslashReplace{$1}/g;
      setBigText $str if $str ne "";
    }
  }elsif(@_ == 1 and $_[0] !~ /\n/ and -e $_[0]){
    my $file = shift;
    open FH, "< $file";
    my $contents = join '', <FH>;
    close FH;
    setBigText $contents;
  }else{
    setBigText "@_";
  }
}

sub python(){
  my $python = '';
  $python = `which python3   2>/dev/null` if $python !~ /python/;
  $python = `which python2.7 2>/dev/null` if $python !~ /python/;
  $python = `which python2.6 2>/dev/null` if $python !~ /python/;
  $python = `which python2   2>/dev/null` if $python !~ /python/;
  $python = `which python    2>/dev/null` if $python !~ /python/;
  chomp $python;
  return $python;
}

sub killBigText(){
  my $python = python();
  system "pkill", "-f", "^$python.*$script";
}

sub isRunning(){
  my $python = python();
  system "pkill", "-0", "-f", "^$python.*$script";
  return $? == 0;
}

sub dbusCall($){
  my $str = shift;
  my $dest = "org.teleshoes.qtbigtext";
  my $path = "/";
  my $method = "setText";
  system "dbus-send", "--session", "--print-reply", "--type=method_call",
    "--dest=$dest",
    $path,
    "$dest.$method",
    "string:$str";
  return $? == 0;
}

sub setBigText($){
  my $str = shift;

  my $dbusOk = 0;
  if(isRunning()){
    print "$script is running, calling dbus\n";
    $dbusOk = dbusCall $str;

    if(not $dbusOk){
      my $retries = $dbusRetries;
      print "dbus call failed, retrying\n";
      while(not $dbusOk and $retries > 0 and isRunning()){
        print "waiting ${retryDelay}s and retrying {$retries retries left}\n";
        sleep $retryDelay;
        $dbusOk = dbusCall $str;
        $retries--;
      }
    }

    print "dbus call failed, killing and rerunning $script\n" if not $dbusOk;
  }

  if(not $dbusOk){
    print "killing $script\n";
    killBigText();
    print "(re)running $script\n";
    my $python = python;
    if(not fork){
      close STDOUT;
      close STDERR;
      open FH, "| $python $script";
      print FH $str;
      close FH;
    }
  }
}

&main(@ARGV);
